    output "bin/badapple.bin"
    org 0x0205
    jp start

    include "debug.z80"

chardefs = 0xF000
screen = 0xF800
outboxFlag = 0x0200
outboxData = 0x0201
inboxFlag = 0x0202
inboxData = 0x0203

cmdFileOpen = 0x40
cmdFileClose = 0x41
cmdFileRead = 0x42
cmdFileSeek = 0x43
cmdFileSeek = 0x43
cmdSetTimerFrequency = 0x50
cmdReset = 0x7F

frequency: dd 1000000/30
bufferFillRate = 105

frameTime: dw 0
time: dw 0
currentFrameSize: dw 0
currentPositionAddress: dw 0

loadAddress: dw 0
loadByteCount: dw bufferFillRate

dataFileName: db "badapple.dat", 0

; -------------------------------------------------------------------------

 macro ExecuteCommand command
    ld a, command
    ld (inboxFlag), a
.commandwaitloop
    ld a, (inboxFlag)
    or a, a
    jr nz, .commandwaitloop
 endm

 macro add_hl_a
    add a, l
    ld l, a
    adc a, h
    sub l
    ld h, a
 endm

; -------------------------------------------------------------------------

start:
    call init

    ; Get the first frame size
    ld hl, 2
    ld (loadByteCount), hl
    ExecuteCommand cmdFileRead
    ld hl, (dataBufferStart)
    ld (loadByteCount), hl

frameLoop:
    ld bc, (frameTime)
    inc bc
    ld (frameTime), bc

timeLoop:
    ld hl, (time)
    or a, a
    sbc hl, bc
    jr c, timeLoop

    ExecuteCommand cmdFileRead
    exx
    ld hl, (loadByteCount)
    exx

    ld de, dataBufferStart
    ld hl, screen
decompressLoop:
    ld a, (de)
    ld b, a
    or a, a
    jp m, runOfChars
runOfSkips:
    add_hl_a
    inc hl
    jp runEnd
runOfChars:
    and a, 7
    inc a
runOfCharsLoop:
    ld (hl), b
    inc hl
    dec a
    jr nz, runOfCharsLoop
runEnd:
    inc de
    exx
    dec hl
    ld a, h
    or a, l
    exx

    jp nz, decompressLoop

    ld hl, (loadByteCount)
    ld de, dataBufferStart
    add hl, de
    dec hl
    ld d, (hl)
    dec hl
    ld e, (hl)
    ld (loadByteCount), de

    ld a, d
    or a, e

    jp nz, frameLoop

    call prepareVideoRamForExit
    ExecuteCommand cmdReset

; -------------------------------------------------------------------------

init:
    call initVariables
    call initInterrupt
    call initTimers
    call initVideoRam
    call initCharDefs
    call openDataFile
    call initInboxData
    ret

; -------------------------------------------------------------------------

initVariables:
    ld hl, dataBufferStart
    ld (loadAddress), hl
    ld (currentPositionAddress), hl
    ret

; -------------------------------------------------------------------------

initInterrupt:
	ld a, 0xC3          ; Opcode for jp
    ld hl, interrupt	; Interrupt handler
    ; Store at NMI vector address
	ld (0x0066), a
	ld (0x0067), hl
    ret

; -------------------------------------------------------------------------

initTimers:
    ld hl, frequency
    ld (inboxData), hl
    ExecuteCommand cmdSetTimerFrequency

    ld hl, 0
    ld (frameTime), hl
    ld (time), hl
    ret

; -------------------------------------------------------------------------

initVideoRam:
    ld bc, 256*8 + 40*30
    ld hl, chardefs
.charloop
    ld (hl), 0
    inc hl
    dec bc
    ld a, b
    or a, c
    jp nz, .charloop
    ret

; -------------------------------------------------------------------------

prepareVideoRamForExit:
    ld hl, chardefs + 32 * 8
    ld b, 8
.charDefLoop
    ld (hl), 0
    inc hl
    djnz .charDefLoop

    ld bc, 40*30
    ld hl, screen
.charloop
    ld (hl), 32
    inc hl
    dec bc
    ld a, b
    or a, c
    jp nz, .charloop
    ret

; -------------------------------------------------------------------------

initCharDefs:
    ld b, 0
    ld hl, chardefs
.charloop
    ld a, 0
    bit 3, b
    jr z, .segmentA
    or a, 15<<4
.segmentA
    bit 4, b
    jr z, .segmentB
    or a, 15
.segmentB
    ld (hl), a
    inc hl
    ld (hl), a
    inc hl
    ld (hl), a
    inc hl
    ld (hl), a
    inc hl

    ld a, 0
    bit 5, b
    jr z, .segmentC
    or a, 15<<4
.segmentC
    bit 6, b
    jr z, .segmentD
    or a, 15
.segmentD
    ld (hl), a
    inc hl
    ld (hl), a
    inc hl
    ld (hl), a
    inc hl
    ld (hl), a
    inc hl

    inc b
    jp nz, .charloop
    ret

; -------------------------------------------------------------------------

openDataFile:
    ld hl, dataFileName
    ld (inboxData), hl
    ExecuteCommand cmdFileOpen
    ret

; -------------------------------------------------------------------------

initInboxData:
    ld hl, loadAddress
    ld (inboxData), hl
    ret

; -------------------------------------------------------------------------

interrupt:
    push af
    push hl
    ld hl, (time)
    inc hl
    ld (time), hl
    pop hl
    pop af
    reti

; -------------------------------------------------------------------------

endOfCode:

endOfMemory = 0xEFFE

dataBufferStart = endOfCode
dataBufferEnd = dataBufferStart + bufferFillRate * ((endOfMemory - dataBufferStart) / bufferFillRate)
